plugins {
    id 'java'
    id 'maven-publish'
}

group 'me.darksidecode.lvq4j'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url 'https://archiva.reflex.rip/repository/public/'

            credentials {
                // ~/.gradle/gradle.properties
                username reflexPrivateUsername
                password reflexPrivatePassword
            }

            authentication {
                basic BasicAuthentication
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'lvq4j'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'LVQ4J'
                description = 'LVQ (Learning Vector Quantization) library for Java'
                url = 'https://github.com/MeGysssTaa/lvq4j'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'DarksideCode'
                        name = 'German Vekhorev'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

def log4jVersion = '2.13.3'

dependencies {
    // Logging
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jVersion}"

    // Annotation processors
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    // Testing
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

/*
 * Copyright 2020 DarksideCode
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// For Lombok
configurations {
    compileOnly.extendsFrom annotationProcessor
}
